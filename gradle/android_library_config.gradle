if (versions == null || versions.publish_version == null) {
    throw new IllegalStateException("Unable to reference publish_version!")
} else if (module_group == null || module_name == null) {
    throw new IllegalStateException("Must provide module_group and module_name!")
}

tasks.register("dokkaJar", Jar) {
    group = "documentation"
    description = "Generates a JAR file with KDoc documentation"
    archiveClassifier.set("${module_name}-javadoc")
    from(tasks.named("dokkaHtml"))
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId module_group
                artifactId module_name
                version versions.publish_version

                // Mostly self-explanatory metadata
                pom {
                    packaging 'aar'
                    name = module_name
                    description = 'Kotlin Extensions (Android extensions) and Helpers for smoother Android development'
                    url = 'https://github.com/leodan11/KExtensions'
                    licenses {
                        license {
                            name = 'Apache 2.0 License'
                            url = 'github.com/leodan11/KExtensions/blob/master/LICENCE'
                        }
                    }
                    developers {
                        developer {
                            id = 'leodan11'
                            name = 'Leonardo D. Palma'
                            email = 'leonardo.palma13@est.unanleon.edu.ni'
                        }
                        // Add all other devs here...
                    }
                    // Version control info - if you're using GitHub, follow the format as seen here
                    scm {
                        connection = 'scm:git:github.com/leodan11/KExtensions.git'
                        developerConnection = 'scm:git:ssh://github.com/leodan11/KExtensions.git'
                        url = 'https://github.com/leodan11/KExtensions/tree/main'
                    }
                }

                artifact(tasks.named("dokkaJar"))
            }
        }
    }
}